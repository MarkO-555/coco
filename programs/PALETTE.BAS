
10 ' MODIFIED RAINBOW PROGRAM TO DISPLAY 64 COLORS AT ONE TIME
20 'JAN. 1987, P.20
30 GOSUB400:WIDTH80
40 POKE&HFFD9,0
50 RGB:CLS5:CLEAR:ONBRKGOTO320:DIMMC(64):M=&HF00:GOSUB200
60 FORI=2TO15:PALETTEI,0:NEXT
70 HSCREEN2:HCLS2:HCOLOR1,2
80 HPRINT(4,1),"Color Computer3 - Color Chart"
90 HPRINT(8,24),"any KEY exits color chart"
100 IFA<3 THENHPRINT(18,2),"RGB" ELSE HPRINT(18,2),"CMP"
110 FORX=0TO3
120 HLINE(X*80+10,32)-(X*80+40,160),PSET,B
130 I=0:FORY=0TO15
140 HPRINT(X*10+5,Y+4),MC(X+I):I=I+4
150 HLINE(X*80+10,Y*8+40)-(X*80+40,Y*8+40),PSET
160 HPAINT(X*80+20,Y*8+36),8+X,1
170 NEXTY,X
180 EXEC&HE00
190 RGB:CLS:HSCREEN0:GOTO50
200 CLS:PRINT"SELECT MONITOR TYPE (R)GB OR (C)MP :";:POKE&H87,0
210 A=PEEK(&H87):IFA=0THEN210
220 A=INSTR("RrCc",CHR$(A)):ONA+1 GOTO200,230,230,310,310
230 PRINT"RGB":FOR R=0 TO 3
240 RM = (R AND 1)*4 +(R AND 2)*16
250 FOR G=0 TO 3
260 GM = (G AND 1)*2 +(G AND 2)*8
270 FOR B=0 TO 3
280 BM = (B AND 1) +(B AND 2)*4
290 MC(Q)=RM + GM + BM:POKEM+Q,MC(Q):Q=Q+1
300 NEXTB,G,R:PALETTE1,63:RETURN
310 PRINT"CMP":FORY=0TO15:FORX=0TO3:B=X*16+Y:MC(Q)=B:POKEM+Q,B:Q=Q+1:NEXTX,Y:PALETTE1,48:RETURN
320 RGB:END
400 REM PAL
410 LI=460
420 FOR M=&HE00 TO &HE43 STEP10:SUM=0
430 FOR I=0TO9:READA$:VA=VAL("&H"+A$):SUM=SUM+VA:POKE M+I,VA:NEXT:READ CHK:IFSUM<>CHK THEN PRINT"ERROR IN LINE"LI:END
440 LI=LI+10:NEXT
450 RETURN
460 DATA 34, 9 , 1A, 50, 7F, FF, 2 , CC, FF, 36, 1064 
470 DATA 1F, 8B, D7, 3 , CC, 10, 46, 33, 8D, 0 , 870 
480 DATA EB, 37, 30, D , 2 , D , 3 , 2A, FC, D , 676 
490 DATA 0 , D , 1 , 2A, FC, 5A, 26, F7, D6, 0 , 897 
500 DATA D6, 1 , 2A, FC, 9F, B8, 10, 9F, BA, 37, 1268 
510 DATA 30, C6, 7 , 4A, 26, E5, 96, 0 , 43, 48, 883 
520 DATA 27, D0, 86, 35, 97, 3 , 35, 89, 00, 00, 778 

