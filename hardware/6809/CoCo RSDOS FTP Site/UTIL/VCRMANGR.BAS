
1 CLS
2 GOTO85
3 X=16-(LEN(XX$)/2)
4 PRINT@PP+X,XX$;
5 RETURN
6 IF P<TP THEN P=TP:P0=1: ELSE IF P>BP THEN P=BP:P0=TN
7 POKEP,62
8 A$=INKEY$:IF A$="" THEN 8
9 IF CHR$(13)=A$ THEN RETURN
10 IF CHR$(94)=A$ THEN P0=P0-1:POKEP,CR:P=P-32
11 IF CHR$(10)=A$ THEN P0=P0+1:POKEP,CR:P=P+32
12 GOTO6
13 PRINT@12,"tape:";TP
14 PRINT@32,STRING$(32,"*")
15 PRINT@96,"M: "M1$
16 PRINT@128,"M: "M2$
17 PRINT@160,"M: "M3$
18 PRINT@192,STRING$(32,"=");
19 PRINT@224,"NAME: "NA$
20 PRINT@256,"AD: "A1$
21 PRINT@288,"AD: "A2$
22 PRINT@320,"PH: "PH$
23 PRINT@352,STRING$(32,"-");
24 PRINT@384,"CHECKED OUT: "CO$
25 PRINT@416,"DATE TO BE RETURNED: "DR$
26 PRINT@448,"TIME: "TI$
27 IF SQ<>1 THEN RETURN
28 PRINT#-2,"TAPE: ";TP
29 PRINT#-2,STRING$(64,"*")
30 PRINT#-2
31 PRINT#-2,"MOVIE #1: ";M1$
32 PRINT#-2,"MOVIE #2: ";M2$
33 PRINT#-2,"MOVIE #3: ";M3$
34 PRINT#-2,STRING$(64,"=")
35 PRINT#-2
36 PRINT#-2,"NAME: ";NA$
37 PRINT#-2,"ADDRESS: ";A1$
38 PRINT#-2,"ADDRESS: ";A2$
39 PRINT#-2,"PHONE NUMBER: ";PH$
40 PRINT#-2,STRING$(64,"-")
41 PRINT#-2
42 PRINT#-2,"CHECKED OUT: ";CO$
43 PRINT#-2,"DATE TO BE RETURNED: ";DR$
44 PRINT#-2,"TIME: ";TI$
45 FOR Q=1TO5:PRINT#-2:NEXTQ
46 RETURN
47 '
48 UB$="":FOR BL=1 TO LB
49 UB$=UB$+CHR$(128)
50 NEXTBL
51 PRINT@PP,UB$;
52 A$=INKEY$:IF A$="" THEN 52
53 IF A$=CHR$(8) THEN 63
54 IF A$=CHR$(&HD) THEN 59
55 AA=INSTR(1,UB$,CHR$(128))
56 IF AA=0 THEN RETURN
57 MID$(UB$,AA,1)=A$
58 GOTO51
59 AA=INSTR(1,UB$,CHR$(128))
60 IF AA=0 THEN RETURN
61 UB$=LEFT$(UB$,AA-1):PRINT@PP,UB$
62 RETURN
63 AA=INSTR(1,UB$,CHR$(128))
64 IF AA=0 OR AA=1 THEN MID$(UB$,LB,1)=CHR$(128):GOTO51
65 MID$(UB$,(AA-1),1)=CHR$(128)
66 GOTO51
67 OPEN"D",#1,"TAPES.DAT",210
68 FIELD #1,28ASM1$,28ASM2$,28ASM3$,25ASNA$,27ASA1$,27ASA2$,27ASPH$,8AS CO$,8ASDR$,4ASTI$
69 RETURN
70 D1$=LEFT$(DD$,2):D2$=MID$(DD$,4,2):D3$=RIGHT$(DD$,2)
71 IF D2$="02" THEN D2=31
72 IF D2$="03" THEN D2=59
73 IF D2$="04" THEN D2=89
74 IF D2$="05" THEN D2=119
75 IF D2$="06" THEN D2=150
76 IF D2$="07" THEN D2=180
77 IF D2$="08" THEN D2=211
78 IF D2$="09" THEN D2=242
79 IF D2$="10" THEN D2=272
80 IF D2$="11" THEN D2=303
81 IF D2$="12" THEN D2=333
82 D2=D2+VAL(D1$):D2$=STR$(D2):D2$=RIGHT$(D2$,(LEN(D2$)-1)):D2$="000"+D2$:D2$=RIGHT$(D2$,3)
83 DD=VAL(D3$+D2$)
84 RETURN
85 CLS6
86 XX$="TAPE INDEX AND LOAN":GOSUB3
87 XX$="MANAGER":PP=32:GOSUB3
88 XX$="WRITTEN AND DEVELOPED":PP=96:GOSUB3
89 XX$="BY":PP=PP+32:GOSUB3
90 XX$="JOHN A. DRUM":PP=PP+32:GOSUB3
91 FOR A=1TO2000:NEXTA
92 CLS4:SQ=0
93 PRINT@96+6,"INVENTORY OF TAPES   ";
94 PRINT@128+6,"SEARCH FOR MOVIE     ";
95 PRINT@160+6,"CHECK TAPES BACK IN  ";
96 PRINT@192+6,"CHECK OUT TAPE       ";
97 PRINT@224+6,"CHECK OVERDUE TAPES  ";
98 PRINT@256+6,"ADD TAPE TO LIST     ";
99 PRINT@288+6,"DELETE TAPE FROM LIST";
100 PRINT@288+38,"PRINT TAPES          ";:PRINT@454,"UP ARROW   DOWN ARROW";
101 TN=8:P0=4:P=1220:CR=191:TP=1124:BP=1348:GOSUB6
102 ON P0 GOTO 103,118,203,216,236,253,279,289
103 'INVENTORY OF TAPES
104 GOSUB67
105 CLS
106 INPUT"START AT WHAT TAPE";TP
107 IF TP>LOF(1) THEN PRINT:PRINT"NUMBER TOO BIG":FOR A=1TO2000:NEXTA:GOTO105
108 IF TP=0 THEN CLOSE:GOTO92
109 GET #1,TP
110 CLS:GOSUB13
111 A$=INKEY$:IF A$="" THEN 111
112 IF A$=CHR$(94) THEN TP=TP-1
113 IF A$=CHR$(10) THEN TP=TP+1
114 IF A$=CHR$(13) THEN CLOSE:GOTO92
115 IF TP<1 THEN TP=1
116 IF TP>LOF(1) THEN TP=LOF(1)
117 GOTO109
118 'SEARCH FOR MOVIE
119 CLS5
120 PRINT@96+6,"SEARCH ALL TAPES ";
121 PRINT@128+6,"SEARCH FOR NUMBER";
122 PRINT@160+6,"SEARCH FOR MOVIE ";
123 PRINT@192+6,"SEARCH FOR NAME  ";
124 PRINT@224+6,"EDIT TAPE INFO.  ";:PRINT@448+4,"UP ARROW   DOWN ARROW";
125 TN=5:P0=3:P=1188:CR=207:TP=1124:BP=1252:GOSUB6
126 ON P0 GOTO 127,128,137,153,167
127 GOSUB67:TP=1:GOTO109
128 CLS
129 GOSUB 67
130 INPUT"ENTER NUMBER OF TAPE";TP
131 IF TP<1 THEN CLOSE:GOTO92
132 IF TP>LOF(1) THEN PRINT:PRINT"NUMBER TOO HIGH":FOR A=1TO2000:NEXTA:CLS:GOTO130
133 CLS
134 GET #1,TP:GOSUB13
135 A$=INKEY$:IF A$="" THEN 135
136 CLOSE:GOTO92
137 CLS
138 PRINT"ENTER NAME OF THE MOVIE TO BE   SEARCHED FOR"
139 LB=28:PP=98:GOSUB47
140 IF UB$="" THEN 92
141 GOSUB67
142 FOR TP=1 TO LOF(1)
143 GET #1,TP
144 IF INSTR(1,M1$,UB$)>0 THEN 149
145 IF INSTR(1,M2$,UB$)>0 THEN 149
146 IF INSTR(1,M3$,UB$)>0 THEN 149
147 NEXT TP
148 PRINT:PRINT"SEARCH STRING NOT FOUND":CLOSE:FOR A=1TO2000:NEXTA:GOTO92
149 CLS:GOSUB13
150 A$=INKEY$:IF A$="" THEN 150
151 NEXT TP
152 CLOSE:GOTO92
153 CLS
154 PRINT"ENTER NAME OF PERSON TO BE      FOUND"
155 LB=25:PP=38:GOSUB47
156 IF UB$="" THEN 92
157 GOSUB67
158 FOR TP=1 TO LOF(1)
159 GET #1,TP
160 IF INSTR(1,NA$,UB$)>0 THEN 163
161 NEXT TP
162 PRINT:PRINT"SEARCH STRING NOT FOUND":FOR A=1TO2000:NEXTA:CLOSE:GOTO92
163 CLS:GOSUB13
164 A$=INKEY$:IF A$="" THEN 164
165 NEXT TP
166 CLOSE:GOTO92
167 CLS
168 INPUT"ENTER TAPE NUMBER THAT IS TO BE EDITED:";TP
169 IF TP<1 THEN 92
170 GOSUB67
171 IF TP>LOF(1) THEN CLOSE:PRINT:PRINT"NUMBER TOO HIGH":FOR A=1TO2000:NEXTA:GOTO167
172 GET #1,TP
173 CLS:GOSUB13
174 INPUT"CORRECT TAPE (Y/N)";A$
175 IF A$<>"Y" THEN CLOSE:GOTO92
176 CLS:GOSUB13
177 PRINT@72,"edit which field";
178 P=1120:PT=PEEK(P):NL=PT-64:P0=1
179 POKE P,NL
180 A$=INKEY$:IF A$="" THEN 180
181 IF A$=CHR$(94) AND P0=1 THEN 180
182 IF A$=CHR$(13) THEN 187
183 IF A$=CHR$(10) AND P0=12 THEN 180
184 IF A$=CHR$(94) THEN POKEP,PT:P=P-32:PT=PEEK(P):NL=PT-64:POKEP,NL:P0=P0-1
185 IF A$=CHR$(10) THEN POKEP,PT:P=P+32:PT=PEEK(P):NL=PT-64:POKEP,NL:P0=P0+1
186 GOTO180
187 POKEP,PT
188 ON P0 GOTO 189,190,191,192,193,194,195,196,197,198,199,200
189 PP=99:LB=28:GOSUB47:LSETM1$=UB$:GOTO176
190 PP=131:LB=28:GOSUB47:LSETM2$=UB$:GOTO176
191 PP=163:LB=28:GOSUB47:LSETM3$=UB$:GOTO176
192 GOTO 201
193 PP=230:LB=25:GOSUB47:LSETNA$=UB$:GOTO176
194 PP=260:LB=27:GOSUB47:LSETA1$=UB$:GOTO176
195 PP=292:LB=27:GOSUB47:LSETA2$=UB$:GOTO176
196 PP=324:LB=27:GOSUB47:LSETPH$=UB$:GOTO176
197 GOTO201
198 PP=397:LB=8:GOSUB47:IF LEN(UB$)<8 THEN 198 ELSE LSET CO$=UB$:GOTO176
199 PP=437:LB=8:GOSUB47:IF LEN(UB$)<8 THEN 199 ELSE LSETDR$=UB$:GOTO176
200 PP=454:LB=4:GOSUB47:LSETTI$=UB$:GOTO176
201 PUT #1,TP
202 CLOSE:GOTO92
203 CLS
204 INPUT"ENTER NUMBER OF TAPE TO BE      CHECKED IN:";TP
205 IF TP<1 THEN 92
206 GOSUB67
207 IF TP>LOF(1) THEN CLOSE:PRINT:PRINT"NUMBER TOO HIGH":FOR A=1TO2000:NEXTA:GOTO203
208 GET #1,TP
209 CLS:GOSUB13
210 XX$="tape check in":PP=64:GOSUB3
211 PRINT@480,"";
212 INPUT"CORRECT TAPE (Y/N)";A$
213 IF A$="Y" THEN 214 ELSE CLOSE:GOTO92
214 LSET NA$="":LSETA1$="":LSETA2$="":LSETPH$="":LSET CO$="":LSETDR$="":LSETTI$=""
215 PUT #1,TP:CLOSE:GOTO92
216 CLS
217 INPUT"ENTER NUMBER TO CHECK OUT:";TP
218 IF TP<1 THEN 92
219 GOSUB67
220 IF TP>LOF(1) THEN CLOSE:PRINT"NUMBER TOO HIGH":FOR A=1TO2000:NEXTA:GOTO216
221 GET #1,TP
222 CLS:GOSUB13
223 INPUT"CORRECT TAPE (Y/N)";A$
224 IF A$="Y" THEN 225 ELSE CLOSE:GOTO92
225 CLS:GOSUB13
226 XX$="tape check out":PP=64:GOSUB3
227 PP=230:LB=25:GOSUB47:LSETNA$=UB$
228 PP=260:LB=27:GOSUB47:LSETA1$=UB$
229 PP=292:LB=27:GOSUB47:LSETA2$=UB$
230 PP=324:LB=27:GOSUB47:LSETPH$=UB$
231 PP=397:LB=8:GOSUB47:IF LEN(UB$)<8 THEN 231 ELSE LSET CO$=UB$
232 PP=437:LB=8:GOSUB47:IF LEN(UB$)<8 THEN 232 ELSE LSET DR$=UB$
233 PP=454:LB=4:GOSUB47:LSETTI$=UB$
234 PUT #1,TP
235 CLOSE:GOTO92
236 CLS:PRINT"ENTER DATE TO CHECK OVER DUE    FOR:":PRINT"EXAMPLE(23/12/86)"
237 PP=37:LB=8:GOSUB47:IF LEN(UB$)<8 THEN 236
238 DD$=UB$:GOSUB70:QQ=DD
239 GOSUB67
240 FOR TP=1TO LOF(1)
241 GET #1,TP
242 IF DR$="        " THEN 245
243 DD$=DR$:GOSUB70
244 IF QQ>DD THEN 247
245 NEXT TP
246 CLOSE:PRINT:PRINT"NO MORE OVERDUE":FOR A=1TO2000:NEXTA:GOTO92
247 CLS:GOSUB13
248 IF SQ=1 THEN 251
249 A$=INKEY$:IF A$="" THEN 249
250 IF A$="Q" THEN CLOSE:GOTO92
251 NEXT TP
252 CLOSE:PRINT:PRINT"NO MORE OVERDUE":FOR A=1TO2000:NEXTA:GOTO92
253 'ADD TAPE TO LIST
254 CLS
255 OPEN"D",#1,"TAPES.DAT",210
256 FIELD #1,28ASM1$,28ASM2$,28ASM3$,25ASNA$,27ASA1$,27ASA2$,27ASPH$,8AS CO$,8ASDR$,4ASTI$
257 FOR TP=1 TO LOF(1)
258 GET #1,TP
259 IF M1$="TAPE DELETED                " THEN 260 ELSE 263
260 PRINT"SLOT NUMBER:"TP"IS OPEN"
261 INPUT"DO YOU WISH TO USE IT(Y/N)";UB$
262 IF UB$="Y" OR UB$="YES" THEN CLS:GOTO265 ELSE 263
263 NEXT TP
264 CLS:TP=LOF(1)+1
265 PRINT"      MOVIE NUMBER 1"
266 PP=33:LB=28:GOSUB47
267 IF UB$="" THEN CLOSE:GOTO92
268 LSET M1$=UB$
269 CLS
270 PRINT"      MOVIE NUMBER 2"
271 GOSUB47:LSET M2$=UB$
272 CLS:PRINT"      MOVIE NUMBER 3"
273 GOSUB 47:LSET M3$=UB$
274 LSETNA$="":LSETA1$="":LSETA2$="":LSETPH$="":LSETCO$="":LSETDR$="":LSETTI$=""
275 CLS:GOSUB13:INPUT"CORRECT (Y/N)";A$
276 IF A$="Y" THEN 277 ELSE CLS:GOTO265
277 PUT #1,TP
278 CLOSE:GOTO92
279 'DELETE TAPE ROUTINE
280 CLS
281 INPUT"TAPE NUMBER TO DELETE";TP
282 IF TP=0 THEN 92
283 INPUT"ARE YOU SURE (Y/N)";A$
284 IF A$="Y" THEN 285 ELSE 92
285 GOSUB67
286 LSETM1$="TAPE DELETED":LSETM2$="":LSETM3$="":LSETNA$="":LSETA1$="":LSETA2$="":LSETPH$="":LSETCO$="":LSETDR$="":LSETTI$=""
287 PUT #1,TP
288 CLOSE:GOTO92
289 CLS4
290 PRINT@96+6,"PRINT ALL TAPES      ";
291 PRINT@128+6,"PRINT NUMBER         ";
292 PRINT@160+6,"PRINT MOVIE          ";
293 PRINT@192+6,"PRINT OVER DUE MOVIES";
294 PRINT@224+6,"PRINT CHECKED OUT    ";
295 PRINT@256+6,"PRINT IN STOCK       ";
296 PRINT@288+6,"RETURN TO MAIN MENU  ";:PRINT@448+6,"UP ARROW   DOWN ARROW";
297 TN=7:P0=4:P=1220:CR=191:TP=1124:BP=1316:GOSUB6
298 ON P0 GOTO 299,305,306,307,308,319,92
299 GOSUB67:TP=1:SQ=1
300 GET #1,TP
301 GOSUB13
302 IF INKEY$="Q" THEN CLOSE:GOTO92
303 IF TP<LOF(1) THEN TP=TP+1:GOTO300
304 CLOSE:GOTO92
305 SQ=1:GOTO128
306 SQ=1:GOTO137
307 SQ=1:GOTO236
308 SQ=1:JP$="YES"
309 GOSUB67
310 FOR TP=1 TO LOF(1)
311 GET #1,TP
312 IF JP$="YES" THEN 313 ELSE 315
313 IF NA$<>"                         " THEN GOSUB 13
314 GOTO317
315 IF JP$="NO" THEN 316 ELSE 92
316 IF NA$="                         " THEN GOSUB13
317 NEXT TP
318 CLOSE:GOTO92
319 SQ=1:JP$="NO":GOTO309
                                                                                                                                                                                  