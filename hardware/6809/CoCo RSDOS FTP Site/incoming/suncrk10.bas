1 'SUNCRACK V1.0 - UTILITY TO *CRACK* (NOT COPY) SOME SUNDOG GAMES
2 'BY JOHN RIDDLE MAY 22, 1997
3 'FIRST BACKUP SUNDOG DISK EXCLUDING TRACK 0
4 'THEN RUN THIS PATCH ON THE BACKUP
5 'METHODS: #1,2,3 & 4 - JSR KILL, #5 - EXEC ADDRESS CHANGE
10 F=0:CLS:WIDTH32
20 PRINT@9,"SUNCRACK V1.0"
30 PRINT:PRINT"INSERT APPROPRIATE GAME DISK    AND SELECT WHICH GAME TO CRACK"
40 PRINT:PRINT"1) SINISTAR","2) THE CONTRAS"
50 PRINT:PRINT"3) PHOTON","4) QUEST/THELDA"
60 PRINT:PRINT"5) PALADIN'S LEGACY"
70 A$=INKEY$:IFA$=""THEN70ELSEIFA$="1"THEN100ELSEIFA$="2"THEN200ELSEIFA$="3"THEN300ELSEIFA$="4"THEN400ELSEIFA$="5"THEN500ELSE70
100 T=30:S=12:O=&H76:GOSUB1000:GOTO2000
200 T=3:S=11:O=&H4B:GOSUB1000:GOTO2000
300 T=34:S=6:O=&H84:GOSUB1000:GOTO2000
400 T=2:S=15:O=&H7E:GOSUB1000:GOTO2000
500 T=1:S=5:O=&HC8:F=1:GOSUB1000:GOTO2000
1000 A=PEEK(&HC006)*256+PEEK(&HC007)
1010 POKE A,2:POKEA+1,0:POKEA+2,T:POKEA+3,S:POKEA+4,4:POKEA+5,0:EXEC55135
1020 IF F=0 THEN POKE 1024+O,&H12:POKE 1024+O+1,&H12:POKE 1024+O+2,&H12
1025 IF F=1 THEN POKE 1024+O,&H32:POKE 1024+O+1,&H51
1030 POKE A,3:POKEA+1,0:POKEA+2,T:POKEA+3,S:POKEA+4,4:POKEA+5,0:EXEC55135
2000 PRINT"THE GAME IS NOW CRACKED."
2010 PRINT"THE DISK CAN NOW BE COPIED USING STANDARD"
2020 PRINT"DISK BACKUP PROCEDURES."
