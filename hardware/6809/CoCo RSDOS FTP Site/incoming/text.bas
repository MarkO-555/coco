
10 'TEXT FILE LISTER FOR COCO DISK BASIC
20 'WILLARD GOOSEY, 1999
30 'goosey@mailhost.nmt.edu
40 'THIS IS COPYRIGHT 1999 WILLARD GOOSEY, RELEASED UNDER GNU GENERAL PUBLIC 
50 'LICENSE 2.0
60 CLEAR 2048
70 DIM D$(2),GR(68),GT(69),GS(69)
80 LINE INPUT "FILENAME> ";F$
90 'PARSE FILENAME FILE CAN BE:
100 'N:NAME.EXT   N:NAME/EXT   NAME.EXT NAME/EXT NAME.EXT:N NAME/EXT:N
110 C=INSTR(1,F$,":")
120 IFC=0THENDR=0:GOTO150
130 IFC=2 THEN F2$=LEFT$(F$,1):DR=VAL(F2$):I=LEN(F$):F2$=RIGHT$(F$,(I-2)):F$=F2$:GOTO 150
140 F2$=RIGHT$(F$,1):DR=VAL(F2$):I=LEN(F$):F$=LEFT$(F$,I-2)
150 C=INSTR(1,F$,"/")
160 IFC=0 THEN 180
170 MID$(F$,C,1)="."
180 C=INSTR(1,F$,".")
190 'PATHOLOGICAL FILE NAMES... NO EXTENTION, SPACES IN NAME
200 IF C=0THENF$=F$+".   " 'DIRECTORY SEARCH EXPECTS A . IN NAME
210 C=INSTR(1,F$,".")
220 IFLEN(F$)<(C+3) THEN F$=F$+" ":GOTO 220 'FILL EXTENTION
230 IFC<9THENF1$=LEFT$(F$,C-1):F2$=RIGHT$(F$,4):F$=F1$+STRING$(8-LEN(F1$)," ")+F2$
240 'READ DIR, FIND FILE
250 DSKI$DR,17,2,D$(1),D$(2) 'READ FAT
260 FA$=LEFT$(D$(1),68)
270 'FORI=1TO68:PRINTHEX$(ASC(MID$(FA$,I,1)));" ";:NEXT I
280 'PRINT
290 FOR SECTOR=3TO11
300 ::DSKI$DR,17,SECTOR,D$(1),D$(2)
310 ::FORJ=1 TO 2
320 ::::FORI=1TO4
330 ::::::F1$=STRING$(8," "):F2$=STRING$(3," ")
340 ::::::OF=(I-1)*32+1
350 ::::::IFMID$(D$(J),OF,1)=CHR$(0)THEN GOTO 440 'DELETED ENTRY
360 ::::::IFMID$(D$(J),OF,1)=CHR$(&HFF)THEN END 'THIS AND ALL FURTHER ENTRIES EMPTY
370 ::::::F1$=MID$(D$(J),OF,8)
380 '::::::PRINT"NAME=";F1$;
390 ::::::F1$=F1$+"."
400 ::::::F1$=F1$+MID$(D$(J),OF+8,3)
410 '::::::PRINT"  EXT =";MID$(D$(J),OF+8,3);
420 '::::::PRINT"  FILE=";F1$
430 ::::::IFF$=F1$THEN GOTO 480
440 ::::NEXT I
450 ::NEXTJ
460 NEXT SECTOR
470 END
480 'PRINT"FOUND";F$,F1$
490 'PRINT"D$(";J;") ENTRY "I
500 DE$=MID$(D$(J),OF,32): 'DIR ENTRY OF FILE WE WANT
510 T=ASC(MID$(DE$,12,1)) 'FILE TYPE BYTE
520 AF=ASC(MID$(DE$,13,1)) 'ASCII FLAG BYTE
530 FG=ASC(MID$(DE$,14,1)) 'FIRST GRANULE NUMBER
540 LS=ASC(MID$(DE$,15,1)) * 256 + ASC(MID$(DE$,16,1)) '# BYTES IN LAST SECTOR
550 'REST OF ENTRY UNUSED, RESERVED FOR FUTURE USE (YEAH RIGHT)
560 'PRINT"TYPE";T;"ASCII";AF;"FIRST GRAN";FG;" (0X";HEX$(FG);") BYTES";LS
570 'BUILD GANULE LIST
580 K=1
590 GR(1)=FG
600 IF GR(K)>=&HC0 THEN GOTO 630
610 ::GR(K+1)=ASC(MID$(FA$,GR(K)+1,1))
620 K=K+1 : GOTO 600
630 'K=NUM OF GRANS +1... GR(K) = NUMBER OF SECTORS USED IN LAST GRAN
640 LG=GR(K) AND &H3F 'BITS 7-6 ARE 1, 5-0 = # SECTORS USED IN LAST GRAN
650 K=K-1
660 'MAP GRANULES -> TRACK,SECTOR
670 'EACH GRANULE = 9 SECTORS = 1/2 TRACK = 2304 BYTES
680 'TRACK 0, SECTOR 1-9 = GRAN 0
690 'TRACK 0, SECTOR 10=18 = GRAN 1 
700 '(...)
710 'TRACK 16, SECTORS 10-18 = GRAN 33
720 'TRACK 17 DIRECTORY--------------
730 'TRACK 18, 1-9 = GRAN 34
740 '(...)
750 'TRACK 34,10-18 = GRAN 67
760 A=0
770 FORL=1TO34 STEP 2 'GRANULE NUMBER +1
780 ::GT(L)=A
790 ::GS(L)=1
800 ::GT(L+1)=A
810 ::GS(L+1)=10
820 ::A=A+1
830 NEXT L
840 A=18
850 FORL=35 TO 67 STEP 2
860 ::GT(L)=A
870 ::GS(L)=1
880 ::GT(L+1)=A
890 ::GS(L+1)=10
900 ::A=A+1
910 NEXT L
920 'YA-TA!  NOW I CAN LIST THE FILE!
930 IFK=1 THEN GOTO 1020
940 FOR L=1 TO K-1
950 ::FOR A= 0 TO 8
960 ::::PRINT"GRANULE ";GR(L);" TRACK";GT(GR(L)+1);"SECTOR";GS(GR(L)+1)+A
970 ::::EXEC 44539
980 ::::DSKI$DR,GT(GR(L)+1),GS(GR(L)+1)+A,P1$,P2$
990 ::::PRINT P1$;P2$
1000 ::NEXT A
1010 NEXT L
1020 IF LG=1 GOTO 1090
1030 FORL=0TO LG-2
1040 ::PRINT"LAST GRANULE: GRANULE ";GR(K);" TRACK";GT(GR(K)+1);"SECTOR";GS(GR(K)+1)+L
1050 ::EXEC 44539
1060 ::DSKI$DR,GT(GR(K)+1),GS(GR(K)+1)+L,P1$,P2$
1070 ::PRINT P1$;P2$
1080 NEXT L
1090 DSKI$DR,GT(GR(K)+1),GS(GR(K)+1)+LG-1,P1$,P2$
1100 PRINT"LAST SECTOR: GRANULE";GR(K);" TRACK";GT(GR(K)+1);"SECTOR";LG+GS(GR(K)+1)-1
1110 EXEC 44539
1120 IF LS<128 THEN PRINTLEFT$(P1$,LS):END
1130 PRINTP1$;
1140 PRINTLEFT$(P2$,LS-128)
1150 END

